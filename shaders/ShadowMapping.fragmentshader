#version 330 core

in vec4 vertex_position_cameraspace;
in vec4 vertex_normal_cameraspace;
in vec4 light_position_cameraspace;
in vec2 vertex_UV;
in vec4 vertex_position_lightspace;
           

uniform sampler2D shadowMapSampler;
uniform sampler2D diffuseColorSampler;
uniform sampler2D specularColorSampler;
uniform sampler2D dudvSampler;

uniform int useTexture; // 0-> material, 1->house, 2-> water


// task 1:
//make river flow
uniform float time;

// light properties
struct Light {
    vec4 La;
    vec4 Ld;
    vec4 Ls;
    vec3 lightPosition_worldspace;
};
uniform Light light;

// materials
struct Material {
    vec4 Ka; 
    vec4 Kd;
    vec4 Ks;
    float Ns; 
};
uniform Material mtl;

out vec4 fragmentColor;


void phong(float visibility);
float ShadowCalculation(vec4 fragPositionLightspace);



void main() {   
    
    // Task 4.3
    float shadow = ShadowCalculation(vertex_position_lightspace);

    float visibility = 1.0f - shadow;

    phong(visibility);
}



float ShadowCalculation(vec4 fragPositionLightspace) {
    // Given the position of a fragment in lightspace coordinates
    // We sample the depth buffer to see whether or not the fragment is shadowed
    
    float shadow;

    // Task 4.3
    
    // Perspective devide to bring coordinates in range[-1, 1]
    vec3 projCoords = vertex_position_lightspace.xyz / vertex_position_lightspace.w;

    // Since the depth map values are in range[0, 1]
    projCoords = 0.5 * projCoords + 0.5;

    // Sampling the closest point in this position from the depth map
    // REMINDER: Since we are in lightspace coordinates,
    //           the z parameter is the depth from the camera
    float closestDepth = texture(shadowMapSampler, projCoords.xy).r;

    // Then we get the depth of the current fragment
    float currentDepth = projCoords.z;

    // If the current fragment is farther than the closest point, the fragment is shadowed
    shadow = (currentDepth > closestDepth) ? 1.0 : 0.0;
  

    // Task 4.4
    // Correcting the quantization problem
    float bias = 0.005;
    shadow = (currentDepth > closestDepth + bias) ? 1.0 : 0.0;
        

    // Task 4.6 Make the shadow edges more realistic

    shadow = 0.0;
    vec2 depthMap_dimensions = textureSize(shadowMapSampler, 0);
    vec2 texelSize = 1.0 / depthMap_dimensions;
    for(int x = -1; x <= 1; x++ ){
        for(int y = -1; y <= 1; y++ ){
            float pcfDepth = texture(shadowMapSampler, projCoords.xy + vec2(x, y) * texelSize).r;
            shadow += currentDepth - bias > pcfDepth ? 1.0 : 0.0;
        }
    }
    shadow /= 9.0;


    // Task 4.5

    if(projCoords.z > 1)
        shadow = 0.0;
    return shadow;

}


void phong(float visibility) {
    vec3 Nused = normalize(vertex_normal_cameraspace.xyz);
    vec2 uv = vertex_UV;
    
    vec4 _Ks = mtl.Ks;
    vec4 _Kd = mtl.Kd;
    vec4 _Ka = mtl.Ka;
    float _Ns = mtl.Ns;
    
    // use texture for HOUSE
    if (useTexture == 1) {
        _Ks = vec4(texture(specularColorSampler, vertex_UV).rgb, 1.0);
        _Kd = vec4(texture(diffuseColorSampler, vertex_UV).rgb, 1.0);
        _Ka = vec4(0.05 * _Kd.rgb, _Kd.a);
        _Ns = 10;
    }
    // model diffuse intensity (Id)
    vec4 N = normalize(vertex_normal_cameraspace); 
    vec4 L = normalize(light_position_cameraspace - vertex_position_cameraspace);

    // for WATER
    if (useTexture == 2) {
        // DuDv animation
        vec2 dudvUV = vec2(vertex_UV.x, vertex_UV.y + time * 0.05);

        vec2 distortion = texture(dudvSampler, dudvUV).rg;
        distortion = distortion * 2.0 - 1.0;
        distortion *= 0.08;

        //micro-noise to be more realistic
        vec2 noiseUV = vertex_UV * 3.0;
        noiseUV.y += time * 0.015;

        vec2 noise = texture(dudvSampler, noiseUV).rg;
        noise = noise * 2.0 - 1.0;
        noise *= 0.015;

        vec2 totalDistortion = distortion + noise;
        vec2 distortedUV = vertex_UV + totalDistortion;
        vec3 baseColor = texture(diffuseColorSampler, distortedUV).rgb;
        // override normal
        N = normalize(vec4(totalDistortion.x, 1.0, totalDistortion.y, 0.0));

        // Fresnel
        vec4 V = normalize(-vertex_position_cameraspace);
        float fresnel = pow(1.0 - max(dot(V, N), 0.0), 3.0);

        vec3 deepWater    = vec3(0.0, 0.18, 0.35);
        vec3 shallowWater = vec3(0.2, 0.5, 0.7);
        vec3 waterColor = mix(deepWater, shallowWater, fresnel);

        _Kd = vec4(baseColor * mix(0.6, 1.0, fresnel), 0.8);
        _Ka = vec4(waterColor * 0.3, 0.8);
        _Ks = vec4(vec3(1.0), 1.0);
        _Ns = mix(40.0, 160.0, fresnel);
   
    }
    // model ambient intensity (Ia)
    vec4 Ia = light.La * _Ka;

    // model diffuse intensity (Id)
    float cosTheta = clamp(dot(N, L), 0, 1);
    vec4 Id = light.Ld * _Kd * cosTheta;

    // model specular intensity (Is)
    vec4 R = reflect(-L, N);
    vec4 E = normalize(vec4(0, 0, 0, 1) - vertex_position_cameraspace);
    float cosAlpha = clamp(dot(E, R), 0, 1);
    float specular_factor = pow(cosAlpha, _Ns);
    vec4 Is = light.Ls * _Ks * specular_factor;

    // here we would normally model the light attenuation effect
    // but since this is a directional light source that is infinitely far away
    // attenuating the light intensity does not make sense

    // final fragment color
    fragmentColor = vec4(
        Ia + 
        Id * visibility +   // Task 4.3 Use visibility
        Is * visibility);
    
    // lowering the opacity of the water
    if (useTexture == 2){
        fragmentColor.a = 0.8; 
    }
}